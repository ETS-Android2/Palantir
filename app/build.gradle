apply plugin: 'com.android.application'
apply plugin: 'com.google.ar.sceneform.plugin'
apply plugin: 'org.ajoberstar.grgit'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "io.ningyuan.palantir"

        // AR Optional apps must declare minSdkVersion >= 14.
        // AR Required apps must declare minSdkVersion >= 24.
        minSdkVersion 24
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        ndk {
            /*
             * Sceneform is available for the following ABIs:
             * 'arm64-v8a', 'armeabi-v7a', 'x86_64' and 'x86'.
             * Your application should include the ABIs most appropriate to
             * minimize APK size. Listing 'arm64-v8a' is recommended.
             *
             * This sample app includes two ABIs:
             * 1. 'arm64-v8a' to run on devices
             * 2. 'x86' to run in the Android emulator
             */
            abiFilters 'arm64-v8a', 'x86'
        }
        externalNativeBuild {
            cmake {
                targets 'tcl', 'vmd'
            }
        }
    }
    // Sceneform libraries use language constructs from Java 8.
    // Add these compile options if targeting minSdkVersion < 26.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
        warningsAsErrors true
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

task fetchVmdSrc {
    description = 'Clone VMD source into src/main/vmd'
    onlyIf { !(new File('./app/src/main/vmd').exists()) }
    doLast {
        org.ajoberstar.grgit.Grgit.clone(dir: "./app/src/main/vmd", uri: "https://github.com/ningyuansg/vmd.git")
        delete "./app/src/main/vmd/.git"
    }
}
preBuild.dependsOn fetchVmdSrc  // fetch VMD before building artifacts

task fetchTclSrc {
    description = 'Clone TCL source into src/main/tcl'
    onlyIf { !(new File('./app/src/main/tcl').exists()) }
    doLast {
        org.ajoberstar.grgit.Grgit.clone(dir: "./app/src/main/tcl", uri: "https://github.com/ningyuansg/tcl8.5.19.git")
        delete "./app/src/main/tcl/.git"
    }
}
preBuild.dependsOn fetchTclSrc  // fetch VMD before building artifacts

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'com.google.ar.sceneform.ux:sceneform-ux:1.6.0'
    implementation 'com.google.ar.sceneform:assets:1.6.0'
    implementation 'commons-io:commons-io:2.6'
    implementation 'de.javagl:jgltf-obj:2.0.0'
}